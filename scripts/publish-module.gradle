apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
    } else {
        // For pure Kotlin libraries
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Kiwrious official Android SDK'
                    url = 'https://github.com/augmented-human-lab/kiwrious-android-library'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/augmented-human-lab/kiwrious-android-library/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'bsc2fast'
                            name = 'Sankha Cooray'
                            email = 'sankha@ahlab.org'
                        }
                        developer {
                            id = 'hsur753'
                            name = 'Hussel Suriyaarachchi'
                            email = 'hussel@ahlab.org'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/augmented-human-lab/kiwrious-android-library.git'
                        developerConnection = 'scm:git:ssh://github.com/augmented-human-lab/kiwrious-android-library.git'
                        url = 'https://github.com/augmented-human-lab/kiwrious-android-library/tree/master'
                    }
                }
            }
        }
    }

}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}